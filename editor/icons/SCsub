#!/usr/bin/env python

Import("env")

import os

import editor_icons_builders


env["BUILDERS"]["MakeEditorIconsBuilder"] = Builder(
    action=env.Run(editor_icons_builders.make_editor_icons_action, "Generating editor icons header."),
    suffix=".h",
    src_suffix=".svg",
)

# Editor's own icons
icon_sources = Glob("*.svg")

# Module icons
for path in env.module_icons_paths:
    icon_sources += (
        Glob(f"{path}/*.svg")
        if os.path.isabs(path)
        else Glob(f"#{path}/*.svg")
    )

env.Alias("editor_icons", [env.MakeEditorIconsBuilder("#editor/editor_icons.gen.h", icon_sources)])
